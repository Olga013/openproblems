import os
import snakemake_tools as tools

rule docker:
    input: tools.push_images
    params:
        version = tools.VERSION_FILE
    shell: "openproblems-cli --version > {params.version}"

rule docker_build:
    input: tools.build_images
    params:
        version = tools.VERSION_FILE
    shell: "openproblems-cli --version > {params.version}"

rule docker_pull:
    input: tools.pull_images

rule docker_update:
    input: tools.update_images

rule update_docker_image:
    input:
        dockerfile = "{}/{{image}}/update.Dockerfile".format(tools.IMAGES_DIR),
    output:
        temp("{}/{{image}}/.docker_update".format(tools.IMAGES_DIR))
    params:
        sourcedir = os.path.dirname(tools.SCRIPTS_DIR),
        user = "singlecellopenproblems",
        build_arg_python = "--build-arg py_requirements=true" if os.path.isfile("{}/{{image}}/requirements.txt".format(tools.IMAGES_DIR)) else "",
        build_arg_r = "--build-arg r_requirements=true" if os.path.isfile("{}/{{image}}/r_requirements.txt".format(tools.IMAGES_DIR)) else "",
    shell:
        """docker build \
        -f {input.dockerfile} \
        -t {params.user}/{wildcards.image} \
        {params.build_arg_python} \
        {params.build_arg_r} \
        .. \
        && touch {output}"""

rule update_dockerfile:
    input:
        docker_pull = "{}/{{image}}/.docker_pull".format(tools.IMAGES_DIR),
    output:
        "{}/{{image}}/update.Dockerfile".format(tools.IMAGES_DIR)
    shell:
        r"""echo '
ARG py_requirements=false
ARG r_requirements=false

FROM singlecellopenproblems/{wildcards.image}:latest AS base

FROM base AS py_requirements_true
# Install Python packages
COPY ./docker/{wildcards.image}/requirements.txt ./requirements.txt
RUN pip install --no-cache-dir -U -r requirements.txt

FROM base AS py_requirements_false
RUN echo "Not installing Python requirements"

FROM py_requirements_${{py_requirements}} AS r_requirements_true
# Install R packages
RUN R -e "install.packages(\"renv\"); renv::consent(TRUE)"
COPY ./docker/{wildcards.image}/r_requirements.txt ./r_requirements.txt
# Install r_requirements.txt
RUN for pkg in $(cat r_requirements.txt); do R -e "renv::install(\"${{pkg}}\")"; done

FROM py_requirements_${{py_requirements}} AS r_requirements_false
RUN echo "Not installing R requirements"

FROM r_requirements_${{r_requirements}} AS r_requirements
# Install single-cell open problems
COPY . /usr/src/singlecellopenproblems
RUN cd /usr/src/singlecellopenproblems && git clean -fxd
RUN pip install --no-cache-dir --editable /usr/src/singlecellopenproblems
' > {output}"""

rule build_docker_image:
    input:
        dockerfile = "{}/{{image}}/Dockerfile".format(tools.IMAGES_DIR),
        requirements = tools.docker_requirements,
    output:
        "{}/{{image}}/.docker_build".format(tools.IMAGES_DIR)
    params:
        sourcedir = os.path.dirname(tools.SCRIPTS_DIR),
        user = "singlecellopenproblems"
    shell:
        """docker build -f {input.dockerfile} -t {params.user}/{wildcards.image} .. \
        && touch {output}"""

rule password_docker:
    output:
        filename = temp(".docker_password")
    run:
        with open(output.filename, 'w') as handle:
            handle.write(tools.DOCKER_PASSWORD)

rule login_docker:
    input:
        ".docker_password"
    output:
        temp(".docker_login")
    shell:
        """docker login --username=singlecellopenproblems --password=$(cat {input}) && \
        touch {output}"""

rule push_docker_image:
    input:
        build = "{}/{{image}}/.docker_build".format(tools.IMAGES_DIR),
        login = ".docker_login",
    output:
        "{}/{{image}}/.docker_push".format(tools.IMAGES_DIR)
    shell:
        "docker push singlecellopenproblems/{wildcards.image} && date +%s > {output}"

rule pull_docker_image:
    output:
        temp("{}/{{image}}/.docker_pull".format(tools.IMAGES_DIR))
    shell:
        "docker pull singlecellopenproblems/{wildcards.image} && touch {output}"
